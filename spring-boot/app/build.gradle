plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.4.2'
    id 'com.diffplug.spotless' version '7.2.1'
}

group = 'app'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.google.zxing:javase:3.5.3'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.6'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.20.0'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'io.rest-assured:rest-assured:5.5.6'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

checkstyle {
    toolVersion = '11.0.1'
    configFile = file("${rootDir}/config/checkstyle/google_checks.xml")
    maxWarnings = 0
    maxErrors = 0
}

spotbugs {
    effort = 'max'
    reportLevel = 'low'
    excludeFilter = file("${rootDir}/config/spotbugs/exclude.xml")
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main.html")
        }
    }
}

spotless {
    java {
        googleJavaFormat('1.28.0')
        removeUnusedImports()
        importOrder()
        endWithNewline()
        trimTrailingWhitespace()
    }
    format 'misc', {
        target '*.gradle', '*.md', '*.xml'
        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
    }
}

tasks.named('check') {
    dependsOn 'spotlessApply'
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

check.dependsOn checkstyleMain, checkstyleTest, spotbugsMain
